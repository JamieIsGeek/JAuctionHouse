plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'net.sootmc'
version = '1.0'
allprojects {
    repositories {
        mavenCentral()
        maven {
            name = "spigotmc-repo"
            url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/groups/public/"
        }
    }
}
dependencies {
    implementation 'com.github.stefvanschie.inventoryframework:IF:0.10.11'
    compileOnly "org.spigotmc:spigot-api:1.19-R0.1-SNAPSHOT"
    compileOnly "org.xerial:sqlite-jdbc:3.8.11.2"
    compileOnly 'com.zaxxer:HikariCP:2.3.2'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    relocate 'com.github.stefvanschie.inventoryframework', 'net.sootmc.jauctionhouse.inventoryframework'
    destinationDirectory.set(file("$buildDir/libs/test-server/plugins"))
}